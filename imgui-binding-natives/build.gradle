plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

def packageName = 'imgui-java-natives-linux'
def packageDesc = 'Native binaries for imgui-java binding for Linux'
def fromDir = '../bin'
def libName = 'libimgui-java64.so'

if (System.getProperty('deployWin')) {
    packageName = 'imgui-java-natives-windows'
    packageDesc = 'Native binaries for imgui-java binding for Windows'
    libName = 'imgui-java64.dll'
} else if (System.getProperty('deployLinux')) {
    if (System.getProperty('deployArm')) {
        packageName = 'imgui-java-natives-linux-arm'
        packageDesc = 'Native binaries for imgui-java binding for Linux with ARM-Based CPU'
        libName = 'libimgui-javaarm64.so'
    } else {
        packageName = 'imgui-java-natives-linux'
        packageDesc = 'Native binaries for imgui-java binding for Linux'
        libName = 'libimgui-java64.so'
    }
} else if (System.getProperty('deployMac')) {
    packageName = 'imgui-java-natives-macos'
    packageDesc = 'Native binaries for imgui-java binding for MacOS'
    libName = 'libimgui-java64.dylib'
}

if (System.getProperty('freetype')) {
    packageName += '-ft'
    packageDesc += ' with Freetype support'
    fromDir += '/freetype'
}

jar {
    from(fromDir) {
        include "$libName" // this is fine
        into 'io/imgui/java/native-bin/'
    }
}

apply from: "$rootDir/publish.gradle"
configurePublishing(packageName, packageDesc)
